(define-module ralph/compiler/free-variables
  import: (ralph/compiler/utilities)
  export: (find-free-variables))

(define-function find-free-variables (exp env)
  (cond
   ((and (instance? exp <array>)
         (not (empty? exp)))
    (bind ((head (first exp))
           (name (and (instance? head <symbol>)
                      (symbol-name head))))
      (select name ==
        (("%method")
         (destructuring-bind (_ name arguments body) exp
           (set-subtract!
            (find-free-variables body env)
            (as-set (concatenate [name] arguments)))))
        (("%bind")
         (destructuring-bind (_ (var value) body) exp
           ;; TODO: make binding available inside value?
           ;; (if so, also change alpha-convert,
           ;;  %bind special form)
           (set-union!
            (set-subtract!
             (find-free-variables body env)
             (as-set [var]))
            (find-free-variables value env))))
        (("%begin")
         (reduce1 set-union!
                  (map (rcurry find-free-variables env)
                       (rest exp))))
        (("%try")
         (destructuring-bind (_ e1 v e2) exp
           (set-subtract!
            (set-union!
             (as-set (find-free-variables e1 env))
             (as-set (find-free-variables e2 env)))
            (as-set [v]))))
        (("%set" "%if" "%while"
          "%array" "%object" "%infix"
          "%get-property" "%native-call" "%return"
          "%plus" "%minus" "%times" "%divide")
         (as-set (reduce concatenate []
                         (map (rcurry find-free-variables env)
                              (rest exp)))))
        (("%native" "%native-name" "%define" "%symbol")
         (as-set []))
        (else:
         (reduce1 set-union!
                  (map (rcurry find-free-variables env)
                       exp))))))
   ((instance? exp <symbol>)
    (unless (get exp "module")
      (signal (concatenate  "NOT QUALIFIED: " (description exp))))
    (as-set [exp]))
   (else:
    (as-set []))))